buildscript {
    ext.kotlin_version = '1.2.71'
    ext.kotlin_coroutine = '0.22.5'
    ext.dokka_version = '0.9.16'
    ext.okhttp = '3.10.0'
    ext.gson = '2.8.5'
    ext.jetty_server = '9.4.12.v20180830'
    ext.junit = '4.12'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'build-dashboard'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile "junit:junit:$junit"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutine"
    compile "com.squareup.okhttp3:okhttp:$okhttp"
    compile "com.google.code.gson:gson:$gson"
    compile "org.eclipse.jetty:jetty-server:$jetty_server"
    compile "org.eclipse.jetty:jetty-webapp:$jetty_server"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dokka {
    includes = ['README.md']
    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/elbekD/kt-telegram-bot/src/main/kotlin'
        suffix = '#L'
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

test {
    useJUnit()

    testLogging {
        displayGranularity = 2
        showStandardStreams = true
        events "failed", "passed", "skipped"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}